buildscript {

    ext {
        springBootVersion = '1.2.3.RELEASE'
        groovyVersion = '2.4.3'
        spockVersion = '1.0-groovy-2.4'
    }
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath 'com.sourcemuse.gradle.plugin:gradle-mongo-plugin:0.4.0'
        classpath("io.spring.gradle:dependency-management-plugin:0.5.0.RELEASE")
    }
}

plugins {
  id "org.grooscript.conversion" version "1.1.2"
}

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'mongo'
apply plugin: 'io.spring.dependency-management'

def compatibilityVersion = 1.7
sourceCompatibility = compatibilityVersion
targetCompatibility = compatibilityVersion

jar {
    baseName = 'rest'
    version =  '1.1.2'
}

grooscript {
    source = ['src/main/groovy/framework/presenter', 'src/main/groovy/framework/model',
            'src/main/groovy/framework/view', 'src/main/groovy/conversion/ConvertedCode.groovy',
            'src/main/groovy/conversion/ConversionPresenter.groovy'
    ]
    destination = 'src/main/resources/static/js/app'
    classPath = ['src/main/groovy']
    initialText = '//Grooscript converted file'
}

repositories {
    mavenCentral()
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-data-mongodb")

    compile "org.codehaus.groovy:groovy-all:${groovyVersion}"
    compile "org.codehaus.groovy:groovy-templates:${groovyVersion}"
    compile('org.grooscript:grooscript:1.1.2') {
        exclude module: 'groovy'
        exclude module: 'groovy-json'
    }

    testCompile 'org.jsoup:jsoup:1.7.3'
    testCompile("org.spockframework:spock-core:$spockVersion") {
        exclude module: 'groovy-all'
    }
    testCompile("org.spockframework:spock-spring:$spockVersion") {
        exclude module: 'groovy-all'
    }
    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile 'cglib:cglib-nodep:3.1'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}

bootRun.dependsOn startManagedMongoDb
bootRun.dependsOn thread
